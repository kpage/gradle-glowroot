repositories {
    mavenCentral()
}

configurations {
    glowrootConfig
}

dependencies {
    glowrootConfig(group: 'org.glowroot', name: 'glowroot-agent', version: '0.9.2', classifier: 'dist', ext: 'zip')
}

// Typically this task is already defined if using the gradle java plugin, this is just for a minimal demo
task clean(type: Delete) {
    delete buildDir
}

// Typically this task is not necessary on a real project if using the gradle java plugin, this is just for a minimal demo
task ensureBuildDir() {
    doLast {
        if(!buildDir.exists()) {
            if(!buildDir.mkdirs()) {
                throw new GradleException("failed to make build directory")
            }
        }
    }
}

task glowrootJavaAgent(type: Copy, dependsOn: ensureBuildDir) {
    from zipTree(project.configurations.glowrootConfig.singleFile)
    into buildDir
}

defaultTasks 'glowrootJavaAgent'